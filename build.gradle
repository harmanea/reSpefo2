plugins {
    id 'java'
    id 'com.diffplug.gradle.eclipse.mavencentral' version '3.17.7' // for swt
    id 'com.palantir.git-version' version '0.12.2' // for git version plugin
}

group 'cz.cuni.mff'
version gitVersion().replace('.dirty', '') // There is a JGit bug

// Default values for running swt locally
// use 'gradle -PswtRelease=<release> -PswtPlatform=<platform> fatJar' to build jars for a target swt platform and version
ext.swtRelease = project.properties['swtRelease'] ?: '4.12.0'
ext.swtPlatform = project.properties['swtPlatform'] ?: 'gtk.linux.x86_64'

eclipseMavenCentral {
    release "$swtRelease", {
        implementation 'org.eclipse.swt'
        implementation 'org.eclipse.jface'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://buchen.github.io/maven-repo' } // for swtchart
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } // for nom-tam-fits snapshot
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name.contains('${osgi.platform}')) {
            details.useTarget(details.requested.group + ':' + details.requested.name.replace('${osgi.platform}', "$swtPlatform") + ':' + details.requested.version)
        }
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'gov.nasa.gsfc.heasarc:nom-tam-fits:1.15.3-SNAPSHOT' // snapshot necessary for header repairs
    implementation 'org.swtchart:org.swtchart:0.10.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.4.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.4.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.4.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.4.1'
    implementation 'org.reflections:reflections:0.9.11'
}

tasks.withType(JavaExec) {
    systemProperty 'respefo.version', version
}

// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Jar.html
task fatJar(type: Jar) {
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'

    manifest {
        attributes(
                'Main-Class': 'cz.cuni.mff.respefo.ReSpefo',
                'Implementation-Version': project.version
        )
    }

    setArchivesBaseName('reSpefo')
    setAppendix("$swtPlatform")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
