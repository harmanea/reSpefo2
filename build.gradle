plugins {
    id 'java'
    id 'com.diffplug.eclipse.mavencentral' version '3.35.0' // for swt
    id 'com.palantir.git-version' version '0.13.0' // for git version plugin
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'cz.cuni.mff'
version gitVersion().replace('.dirty', '') // There is a JGit bug

// Default values for running swt locally
// use 'gradle -PswtRelease=<release> -PswtPlatform=<platform> fatJar' to build jars for a target swt platform and version
ext.swtRelease = project.properties['swtRelease'] ?: '4.17.0'
ext.swtPlatform = project.properties['swtPlatform'] ?: 'gtk.linux.x86_64'

eclipseMavenCentral {
    release "$swtRelease", {
        implementation 'org.eclipse.swt'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://buchen.github.io/maven-repo' } // for swtchart
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.name.contains('${osgi.platform}')) {
            details.useTarget(details.requested.group + ':' + details.requested.name.replace('${osgi.platform}', "$swtPlatform") + ':' + details.requested.version)
        }
    }
}

dependencies {
    implementation 'junit:junit:4.13.2'
    implementation 'gov.nasa.gsfc.heasarc:nom-tam-fits:1.16.1'
    implementation 'org.swtchart:org.swtchart:0.10.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.0'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.14.0'
    implementation 'org.reflections:reflections:0.10.2'
    implementation 'org.slf4j:slf4j-nop:2.0.3'
}

tasks.withType(JavaExec) {
    systemProperty 'respefo.version', version
}

// https://docs.gradle.org/current/dsl/org.gradle.api.tasks.bundling.Jar.html
task fatJar(type: Jar) {
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'

    manifest {
        attributes('Main-Class': 'cz.cuni.mff.respefo.ReSpefo',
                   'Implementation-Version': project.version)

    }

    archiveBaseName = 'reSpefo'
    archiveAppendix = "$swtPlatform"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
